%%[

/*Retrieve fields from Sending DE */
SET @custID = [CustomerID]
SET @firstName = [FirstName]


/* First name and subject line logic */
IF NOT EMPTY(@firstName) THEN 
   SET @firstNameTrim = Trim(@firstName)
   SET @finalName = ProperCase(@firstNameTrim)
   SET @subjectLine = CONCAT(@finalName, ",", " Happy 1 year! We have a special gift for you ðŸŽ")
ELSE 
   SET @finalName = "there"
   SET @subjectLine = "Happy 1 year! We have a special gift for you ðŸŽ"
ENDIF

/* Look up rows */
SET @rows = LookupRows("MarketingCustomerView", "CustomerID", @custID)
SET @rowCount = rowcount(@rows)

/* Check if there is a row that exists */
IF @rowCount > 0 THEN

  SET @row = row(@rows,1)  
  SET @totalYearlySpend = field(@row, "TotalYearlySpend")
  SET @avgBasketSize = field(@row, "AvgBasketSize")
  
  /* DISCOUNT LOGIC ON TOTAL YEARLY SPEND AND AVERAGE BASKET SIZE */
  /* discount code for customers with <= 499 yearly spend */
  IF @totalYearlySpend <= 499 THEN
     IF @avgBasketSize <= 49 THEN 
        SET @discount = "10"
        SET @discountRef = "GTUI90"
     ELSEIF @avgBasketSize <= 99 THEN
        SET @discount = "15"
        SET @discountRef = "TGI782"       
     ELSE
        SET @discount = "20"
        SET @discountRef = "THP892"       
     ENDIF    
        
        
   /* discount code for customers with <= 999 yearly spend */  
  ELSEIF @totalYearlySpend <= 999 THEN
     IF @avgBasketSize <= 49 THEN
        SET @discount = "15" 
        SET @discountRef = "POE231"   
     ELSEIF @avgBasketSize <=99 THEN 
        SET @discount = "20"
        SET @discountRef = "IIO908"       
     ELSE 
        SET @discount = "25"
        SET @discountRef = "THN567"           
     ENDIF
        
          
  /* discount code for customers with >= 1000 yearly spend */        
  ELSE
     IF @avgBasketSize <= 49 THEN
        SET @discount = "20"
        SET @discountRef = "UIT897"        
     ELSEIF @avgBasketSize <= 99 THEN
        SET @discount = "25"
        SET @discountRef = "WEX312"            
     ELSE 
        SET @discount = "30"
        SET @discountRef = "TTN187"
        
    ENDIF      
  ENDIF    
  
ELSE  
  RaiseError("No row was found")  
  
ENDIF

/* Generate link */
SET @discountLink = CONCAT("fitgear.com/redeem?code=", @discountRef)
    
  /* Set unsubscibe link */
SET @unsubscribeLink = "www.unsubscribe.com"

/* Dynamic Message */
IF @avgBasketSize <= 49 THEN
  SET @emailMessage = CONCAT("Thanks for being with us for a year! Enjoy ", @discount, "% off your next purchase as a token of our appreciation. We hope you continue to find everything you need for your sports and fitness journey.")
ELSEIF @avgBasketSize <= 99 THEN
  SET @emailMessage = CONCAT("It's been a fantastic year with you! To celebrate, we're giving you ", @discount, "% off your next purchase. Thank you for choosing FitGear for your sports needs.")
ELSE
  SET @emailMessage = CONCAT("A big thank you for being with us for a year! Enjoy ", @discount, "% off your next purchase. We're thrilled to have you as a loyal customer.")
ENDIF

]%%



